{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "8a4d5027",
   "metadata": {},
   "outputs": [],
   "source": [
    "# import Pkg; Pkg.add([\"SimpleGraphs\",\"JSON\",\"TikzGraphs\",\"TikzPictures\",\"Graphs\",\"DataStructures\"])\n",
    "import JSON\n",
    "import TikzGraphs, TikzPictures\n",
    "using SimpleGraphs\n",
    "using Graphs\n",
    "using DataStructures"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "dfd0ccb7",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "interval (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "#(true,<reason>) if known true, (false,<reason>) if known false\n",
    "#proof is a list of strings naming theorems, and then an integer giving the rough 'deepness' of this proof.\n",
    "struct KnownType\n",
    "    valid::Bool\n",
    "    proof::Vector{String}\n",
    "    priority::Int\n",
    "end\n",
    "\n",
    "struct LangPoset\n",
    "    v::Set{String} #known classes in the poset\n",
    "    e::Dict{Tuple{String,String}, KnownType} #set of facts (x,y) where x ⊆ y or not.\n",
    "end\n",
    "#create empty LangPoset\n",
    "function LangPoset()\n",
    "    return LangPoset(Set{String}(), Dict{Tuple{String,String}, KnownType}())\n",
    "end\n",
    "#create LangPoset on given classes, but no relations\n",
    "function LangPoset(names::Vector{String})::LangPoset\n",
    "    poset = LangPoset()\n",
    "    for name = names\n",
    "        addVertPoset!(poset, name)\n",
    "    end\n",
    "    return poset\n",
    "end\n",
    "\n",
    "import Base.in, Base.getindex, Base.setindex!\n",
    "#for checking if a class / relation is in the Poset\n",
    "in(x::String, poset::LangPoset) = in(x, poset.v)\n",
    "in(xy::Tuple{String, String}, poset::LangPoset) = in(xy, keys(poset.e))\n",
    "getindex(poset::LangPoset, x::String, y::String) = poset.e[x,y]\n",
    "setindex!(poset::LangPoset, thm::KnownType, x::String, y::String) = (poset.e[x,y] = thm)\n",
    "\n",
    "function addVertPoset!(P::LangPoset, name::String)::Nothing\n",
    "    push!(P.v, name)\n",
    "    \n",
    "    #every item is a subset of itself, no theorems needed. Deepness -1.\n",
    "    knowObj::KnownType = KnownType(true, [], -1)\n",
    "    P[name, name] = knowObj\n",
    "    return\n",
    "end\n",
    "\n",
    "#List of classes that contain v, x⊇v. \n",
    "function above(P::LangPoset, v::String)::Vector{String}\n",
    "    if v ∉ P\n",
    "        error(\"Class not in poset \", v)\n",
    "    end\n",
    "    els = filter(P.e) do pp\n",
    "        lhs, rhs = pp.first\n",
    "        known = pp.second\n",
    "       (lhs == v) && (known.valid == true)\n",
    "    end\n",
    "    return map(x -> x[2], collect(keys(els)))\n",
    "end\n",
    "\n",
    "#List of classes that v contains, x⊆v. \n",
    "function below(P::LangPoset, v::String)::Vector{String}\n",
    "    if v ∉ P\n",
    "        error(\"Class not in poset \", v)\n",
    "    end\n",
    "    els =  filter(P.e) do pp\n",
    "        lhs, rhs = pp.first\n",
    "        known = pp.second\n",
    "       (rhs == v) && (known.valid == true)\n",
    "    end\n",
    "    return map(x -> x[1], collect(keys(els)))\n",
    "end\n",
    "\n",
    "#List of classes that definitely do not contain v, x⊉v.\n",
    "function notabove(P::LangPoset, v::String)::Vector{String}\n",
    "    if v ∉ P\n",
    "        error(\"Class not in poset \", v)\n",
    "    end\n",
    "    els =  filter(P.e) do pp\n",
    "        lhs, rhs = pp.first\n",
    "        known = pp.second\n",
    "       (lhs == v) && (known.valid == false)\n",
    "    end\n",
    "    return map(x -> x[2], collect(keys(els)))\n",
    "end\n",
    "\n",
    "#List of classes that v definitely does not contain, x⊈v.\n",
    "function notbelow(P::LangPoset, v::String)::Vector{String}\n",
    "    if v ∉ P\n",
    "        error(\"Class not in poset \", v)\n",
    "    end\n",
    "    els =  filter(P.e) do pp\n",
    "        lhs, rhs = pp.first\n",
    "        known = pp.second\n",
    "       (rhs == v) && (known.valid == false)\n",
    "    end\n",
    "    return map(x -> x[1], collect(keys(els)))\n",
    "end\n",
    "\n",
    "#Are classes x and y equal in P?\n",
    "function is_equal(P::LangPoset, x::String, y::String)::Bool\n",
    "  return (x,y) ∈ P && P.e[x,y].valid &&\n",
    "        (y,x) ∈ P && P.e[y,x].valid\n",
    "end\n",
    "\n",
    "#Are classes x and y definitely unequal in P?\n",
    "function is_unequal(P::LangPoset, x::String, y::String)::Bool\n",
    "  return ((x,y) ∈ keys(P.e) && !P.e[x,y].valid) ||\n",
    "        ((y,x) ∈ keys(P.e) && !P.e[y,x].valid)\n",
    "end\n",
    "\n",
    "#List of classes that v is known equal to\n",
    "function equal(P::LangPoset, v::String)::Set{String}\n",
    "    if v ∉ P.v\n",
    "        error(\"Class not in poset \", v)\n",
    "    end\n",
    "    return filter(P.v) do x\n",
    "        is_equal(P, v, x)\n",
    "    end\n",
    "end\n",
    "\n",
    "#List of classes that v is known unequal to\n",
    "function unequal(P::LangPoset, v::String)::Set{String}\n",
    "    if v ∉ P[\"v\"]\n",
    "        error(\"Class not in poset \", v)\n",
    "    end\n",
    "    return filter(P.v) do x\n",
    "        is_unequal(P, v, x)\n",
    "    end\n",
    "end\n",
    "\n",
    "function synth_thm(thm1::KnownType, thm2::KnownType)::KnownType\n",
    "    @assert thm1.valid || thm2.valid\n",
    "    valid = thm1.valid && thm2.valid\n",
    "    proof = vcat(thm1.proof, thm2.proof)\n",
    "    priority = max(thm1.priority, thm2.priority)\n",
    "    return KnownType(valid, proof, priority)\n",
    "end\n",
    "\n",
    "synth_thm(thm1::KnownType, thm2::KnownType, thm3::KnownType)::KnownType = synth_thm(thm1, synth_thm(thm2, thm3))\n",
    "\n",
    "#given a theorem that implies that x⊆y, try to add it in.\n",
    "#if it's a higher deepness than existing knowledge, skip.\n",
    "#If it's x !⊆ y, then set 'fact' to false.\n",
    "#if it conflicts with given info, gives an error.\n",
    "function add_thm!(P::LangPoset, x::String, y::String, thm::KnownType)::Nothing\n",
    "    if (x,y) ∈ keys(P.e) && P.e[x,y].valid != thm.valid\n",
    "        error(\"Conflicting info, that (x,y) has \",fact,\" but \",thm,\" says otherwise.\")\n",
    "    end\n",
    "    \n",
    "    if (x,y) ∈ keys(P.e)\n",
    "        oldThm = P.e[x,y]\n",
    "        if oldThm.priority < thm.priority\n",
    "            return #already a lower priority one there, skip.\n",
    "        end\n",
    "        if (oldThm.priority == thm.priority) && (length(oldThm.proof) <= length(thm.proof))\n",
    "            return #already have one of equal priority and no longer, skip.\n",
    "        end\n",
    "    end\n",
    "    #otherwise add it in\n",
    "    P.e[x,y] = thm\n",
    "    return\n",
    "end\n",
    "\n",
    "function add_leq!(P::LangPoset, x::String, y::String, thm::String, priority::Int)\n",
    "    known = KnownType(true, [thm], priority)\n",
    "    \n",
    "    #We learn that x⊆y. Then for and d⊆x and y⊆u, we know d⊆u.\n",
    "    U = above(P,y)\n",
    "    D = below(P,x)\n",
    "  \n",
    "    push!(U,y)\n",
    "    push!(D,x)\n",
    "    for u in U\n",
    "        for d in D\n",
    "            \n",
    "            uThm = P.e[y,u]\n",
    "            dThm = P.e[d,x]\n",
    "            newThm = synth_thm(dThm, known, uThm)\n",
    "            \n",
    "            add_thm!(P, d, u, newThm)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function add_equals!(P::LangPoset, x::String, y::String, thm::String, priority::Int)\n",
    "  add_leq!(P,x,y,thm,priority)\n",
    "  add_leq!(P,y,x,thm,priority)\n",
    "end\n",
    "\n",
    "# ⊈\n",
    "function add_notleq!(P::LangPoset, x::String, y::String, thm::String, priority::Int)\n",
    "    known = KnownType(false, [thm], priority)\n",
    "    \n",
    "    #We learn that x⊈y, that is, there's an element of x not in y.\n",
    "    #Then for and x⊆u and d⊆y, we know that element is in u and not d,\n",
    "    #so u⊈d.\n",
    "    U = above(P,x)\n",
    "    D = below(P,y)\n",
    "    \n",
    "    push!(U,x)\n",
    "    push!(D,y)\n",
    "    for u in U\n",
    "        for d in D\n",
    "            \n",
    "            uThm = P.e[x,u]\n",
    "            dThm = P.e[d,y]\n",
    "            newThm = synth_thm(dThm, known, uThm)\n",
    "            \n",
    "            add_thm!(P, u, d, newThm)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function interval(P::LangPoset, x::String, y::String)\n",
    "  A = Set(above(P,x))\n",
    "  B = Set(below(P,y))\n",
    "  C = Set(below(P,x))\n",
    "  D = Set(above(P,y))\n",
    "  return collect(setdiff(intersect(A,B),union(C,D)))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "7aa2e546",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "prettyPrint (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function prettyPrintClass(class)\n",
    "  println(class[\"name\"],\" -- \",class[\"type\"])\n",
    "  println(\"  \",class[\"desc\"])\n",
    "  if \"solver\" in keys(class)\n",
    "    println(\"  Def: \",class[\"solver\"])\n",
    "  end\n",
    "  if \"related\" in keys(class)\n",
    "    println(\"  See Also: \",join(class[\"related\"],\", \"))\n",
    "  end\n",
    "end\n",
    "\n",
    "function prettyPrint(db)\n",
    "  for class = db[\"classes\"]\n",
    "    prettyPrintClass(class)\n",
    "  end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "67701db1",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "export_classes_for_web (generic function with 2 methods)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function make_language_poset(classlist=language_classes; verbose=false)\n",
    "  #Assemble lattice of known inclusions and nonequalities\n",
    "  #class_lattice = LanguagePoset(collect(keys(classlist)))\n",
    "  class_lattice = LangPoset(collect(keys(classlist)))\n",
    "\n",
    "  for (thmname, thm) = theorems\n",
    "        \n",
    "    #Simple 'priority' assignment based on the 'impliedby' and giving ObviousConstruction priority.\n",
    "    #If a theorem has a `priority` field and `impliedby`, it uses the `priority` as an override.\n",
    "    rootThm = thm\n",
    "    while \"impliedby\" ∈ keys(rootThm) && \"priority\" ∉ keys(rootThm)\n",
    "        rootThm = theorems[rootThm[\"impliedby\"]]\n",
    "    end\n",
    "    if \"priority\" ∈ keys(rootThm)\n",
    "        priority = rootThm[\"priority\"]\n",
    "    else\n",
    "        priority = 1\n",
    "    end\n",
    "    \n",
    "    content = thm[\"content\"]\n",
    "    if content == \"\"\n",
    "      continue #no content implemented\n",
    "    end\n",
    "    if content[1] == '{'\n",
    "      verbose && println(\"Skipping statement \",content)\n",
    "      continue #depends on arguments, not implemented\n",
    "    end\n",
    "    #dead simple \"parsing\"\n",
    "    for part = split(content,\"&&\")\n",
    "      if part[1] == '{'\n",
    "        verbose && println(\"Skipping statement \",part,\" from \",content)\n",
    "        continue #depends on arguments, not implemented\n",
    "      end\n",
    "      rel = first.(String.(getfield.(collect(eachmatch(r\"[⊂⊆⊃⊇⊈⊉⊊≠=]\", part)), :match)))\n",
    "      if rel === nothing\n",
    "        println(\"Unknown statement \",part)\n",
    "        continue\n",
    "      end\n",
    "      @assert length(rel) == 1 content*\" has multiple operators \"*string(rel)\n",
    "      rel = rel[1]\n",
    "      lhs, rhs = String.(split(part, rel))\n",
    "      @assert lhs in keys(classes) lhs, \"Language \"*lhs*\" not known, in theorem \"*thmname\n",
    "      @assert rhs in keys(classes) rhs, \"Language \"*rhs*\" not known, in theorem \"*thmname\n",
    "      if lhs ∉ keys(classlist) && lhs ∈ keys(classes)\n",
    "        if verbose\n",
    "          println(\"Skipping \",part,\" for being wrong language type\")\n",
    "        end\n",
    "        continue\n",
    "      end\n",
    "      if rel == '⊊' #normalize subsetnoteq\n",
    "        rel = '⊂'\n",
    "      end\n",
    "      #normalize lhs->rhs ordering\n",
    "      if rel == '⊃'\n",
    "        rel = '⊂'\n",
    "        lhs,rhs = rhs,lhs\n",
    "      elseif rel == '⊇'\n",
    "        rel = '⊆'\n",
    "        lhs,rhs = rhs,lhs\n",
    "      elseif rel == '⊉'\n",
    "        rel = '⊈'\n",
    "        lhs,rhs = rhs,lhs\n",
    "      end\n",
    "      #Use the operator to modify poset appropriately\n",
    "        if rel == '⊆'\n",
    "            verbose && println(\"Add \",lhs,\" ---> \",rhs)\n",
    "            add_leq!(class_lattice, lhs, rhs, thmname, priority)\n",
    "        elseif rel == '='\n",
    "            verbose && println(\"Add \",lhs,\" <==> \",rhs)\n",
    "            add_equals!(class_lattice, lhs, rhs, thmname, priority)\n",
    "        elseif rel == '⊈'\n",
    "            verbose && println(\"Add \",lhs,\" -/-> \",rhs)\n",
    "            add_notleq!(class_lattice, lhs, rhs, thmname, priority)\n",
    "        elseif rel == '⊂'\n",
    "            verbose && println(\"Add \",lhs,\" -/-> \",rhs)\n",
    "            add_leq!(class_lattice, lhs, rhs, thmname, priority)\n",
    "            add_notleq!(class_lattice, rhs, lhs, thmname, priority)\n",
    "        elseif rel == '≠'\n",
    "            #do nothing, handled in is_neq\n",
    "            println(\"Relation ≠ in \", thmname,\" should be replaced with ⊈ or ⊉.\")\n",
    "        else\n",
    "            println(\"Strange relation \",rel)\n",
    "        end\n",
    "    end\n",
    "  end\n",
    "  #Now go through class equalities and normalize.\n",
    "  return class_lattice\n",
    "end\n",
    "\n",
    "function LangPoset_to_SimpleDiGraph(poset; verbose=false)\n",
    "  vertlist = collect(poset.v)\n",
    "  v_to_i = v -> findfirst(x->x==v, vertlist)\n",
    "  edge_list = Edge{Int}[]\n",
    "  for v1 = vertlist\n",
    "    i1 = v_to_i(v1)\n",
    "    for v2 = above(poset, v1)\n",
    "      if v1 == v2\n",
    "        continue #skip self-loops\n",
    "      end\n",
    "      if v1 ∈ above(poset, v2)\n",
    "        continue #skip equality\n",
    "      end\n",
    "      i2 = v_to_i(v2)\n",
    "      if length(interval(poset, v1, v2)) > 0\n",
    "        verbose && println(\"skip \",v1,\" to \",v2)\n",
    "        continue\n",
    "      end\n",
    "      verbose && println(\"add \",v1,\" to \",v2)\n",
    "      push!(edge_list, Graphs.SimpleEdge{Int}(i1,i2))\n",
    "    end\n",
    "    end\n",
    "  SimpleDiGraph(edge_list), vertlist\n",
    "end\n",
    "\n",
    "function export_classes_for_web(class_lattice, langtype=\"Language\")\n",
    "    priority = 0 #TODO, for now all are equal priority\n",
    "    \n",
    "    #First filter out any classes that are equal to something else, except for the representatives of each\n",
    "    equal_reps = Any[] #things to include\n",
    "    equal_skips = Any[] #things to not\n",
    "    for class_obj = db[\"classes\"]\n",
    "        if class_obj[\"type\"] != langtype\n",
    "            continue\n",
    "        end\n",
    "        class = class_obj[\"name\"]\n",
    "        \n",
    "        #equal to something else?\n",
    "        equals = collect(equal(class_lattice, class))\n",
    "        if length(equals) > 1 #there's something else it's equal to\n",
    "            canon_rep = intersect(equals, canonical_forms)\n",
    "            if length(canon_rep) > 1\n",
    "                error(\"Multiple representatives \",canon_rep,\" for equal classes \",equals)\n",
    "            end\n",
    "            if length(canon_rep) == 1\n",
    "                canon_rep = canon_rep[1]\n",
    "            else\n",
    "                sort!(equals)\n",
    "                canon_rep = equals[1]\n",
    "                if canon_rep == class\n",
    "                    println(\"Choosing \",class,\" as representative for \",equals)\n",
    "                end\n",
    "            end\n",
    "            if canon_rep != class\n",
    "                continue\n",
    "            end\n",
    "        end\n",
    "        setdiff!(equals, [class]) #delete self from list of equals\n",
    "        \n",
    "        push!(equal_reps, class)\n",
    "        append!(equal_skips, equals)\n",
    "    end\n",
    "    \n",
    "    class_list = Any[]\n",
    "    for class_obj = db[\"classes\"]\n",
    "        class = class_obj[\"name\"]\n",
    "        \n",
    "        if class_obj[\"type\"] != langtype #TODO: non-language classes\n",
    "            continue\n",
    "        end\n",
    "        \n",
    "        children = Any[]\n",
    "        #only collect children if it's not skipped\n",
    "        is_equal = class ∈ equal_skips\n",
    "        if !is_equal\n",
    "            \n",
    "            for class2 = above(class_lattice, class) #loop over neighbors\n",
    "                if class == class2 #skip over self\n",
    "                    continue\n",
    "                end\n",
    "                if class2 ∈ equal_skips #skip over non-representative classes\n",
    "                    continue\n",
    "                end\n",
    "                if length(interval(class_lattice, class, class2)) > 0 #skip over non-immediate children\n",
    "                    continue\n",
    "                end\n",
    "                #TODO: add edge info on each child\n",
    "                push!(children, class2)\n",
    "            end\n",
    "            \n",
    "            equals = equal(class_lattice, class)\n",
    "            setdiff!(equals, [class])\n",
    "        \n",
    "        else\n",
    "            equals = false\n",
    "            \n",
    "        end\n",
    "\n",
    "        desc = class_obj[\"desc\"]\n",
    "        related = class_obj[\"related\"]\n",
    "        properties = class_obj[\"properties\"]\n",
    "        notes = \"notes\" ∈ keys(class_obj) ? class_obj[\"notes\"] : \"\"\n",
    "        \n",
    "        #TODO: \"related\", \"notes\", \"properties\"\n",
    "        export_obj = Dict(\n",
    "            \"name\"=>class,\n",
    "            \"desc\"=>desc,\n",
    "            \"children\"=>children,\n",
    "            \"equals\"=>equals,\n",
    "            \"related\"=>related,\n",
    "            \"notes\"=>notes,\n",
    "            \"properties\"=>properties\n",
    "            )\n",
    "        push!(class_list, export_obj)\n",
    "    end\n",
    "    \n",
    "    class_list\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 36,
   "id": "fd1786a5",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Initial checks passed\n"
     ]
    }
   ],
   "source": [
    "#useful characters: ≤ ≥ ⊂ ⊆ ⊃ ⊇ ≠ = ∩ ⟹ Ω Σ Π Δ\n",
    "\n",
    "# s_raw = read(\"classes.json\", String)\n",
    "# s = replace(s_raw, r\"\\n[ \\t]*//.*\" => \"\\n\") #strip comments\n",
    "# db = JSON.parse(s);\n",
    "db_pt = JSON.parse(read(\"data/problem_types.json\", String))\n",
    "db_pr = JSON.parse(read(\"data/properties.json\", String))\n",
    "db_cl = JSON.parse(read(\"data/classes.json\", String))\n",
    "db_co = JSON.parse(read(\"data/conjectures.json\", String))\n",
    "db_th = JSON.parse(read(\"data/theorems.json\", String))\n",
    "db_rf = JSON.parse(read(\"data/references.json\", String))\n",
    "\n",
    "db = Dict(\n",
    "    \"problem types\" => db_pt, \"properties\" => db_pr, \n",
    "    \"classes\" => db_cl, \"conjectures\" => db_co,\n",
    "    \"theorems\" => db_th, \"references\" => db_rf)\n",
    "\n",
    "#Verify the database makes reasonable sense\n",
    "\n",
    "@assert all(\"name\" in keys(t) for t in db[\"problem types\"])\n",
    "@assert all(\"desc\" in keys(t) for t in db[\"problem types\"])\n",
    "\n",
    "@assert all(\"name\" in keys(t) for t in db[\"properties\"])\n",
    "@assert all(\"desc\" in keys(t) for t in db[\"properties\"])\n",
    "\n",
    "@assert all(\"name\" in keys(t) for t in db[\"classes\"])\n",
    "@assert all(\"type\" in keys(t) for t in db[\"classes\"])\n",
    "@assert all(\"desc\" in keys(t) for t in db[\"classes\"])\n",
    "\n",
    "@assert all(\"name\" in keys(t) for t in db[\"conjectures\"])\n",
    "@assert all(\"content\" in keys(t) for t in db[\"conjectures\"])\n",
    "\n",
    "@assert all(\"name\" in keys(t) for t in db[\"theorems\"])\n",
    "@assert all(\"content\" in keys(t) for t in db[\"theorems\"])\n",
    "\n",
    "@assert all(\"name\" in keys(t) for t in db[\"references\"])\n",
    "@assert all(\"desc\" in keys(t) for t in db[\"references\"])\n",
    "@assert all(\"url\" in keys(t) for t in db[\"references\"])\n",
    "\n",
    "problem_types = Dict((x->x[\"name\"]=>x).(db[\"problem types\"]))\n",
    "classes = Dict((x->x[\"name\"]=>x).(db[\"classes\"]))\n",
    "conjectures = Dict((x->x[\"name\"]=>x).(db[\"conjectures\"]))\n",
    "theorems = Dict((x->x[\"name\"]=>x).(db[\"theorems\"]))\n",
    "properties = Dict((x->x[\"name\"]=>x).(db[\"properties\"]))\n",
    "references = Dict((x->x[\"name\"]=>x).(db[\"references\"]))\n",
    "\n",
    "@assert all(t[\"type\"] in keys(problem_types) for (n,t) in classes)\n",
    "\n",
    "for (clsname, cls) = classes\n",
    "    if \"related\" ∉ keys(cls)\n",
    "        cls[\"related\"] = Set{String}()\n",
    "    end\n",
    "    cls[\"related\"] = Set{String}(cls[\"related\"])\n",
    "    for rlt = cls[\"related\"]\n",
    "        @assert (rlt ∈ keys(classes)) (clsname*\" related to unknown class \"*rlt)\n",
    "    end\n",
    "    \n",
    "    if \"properties\" ∉ keys(cls)\n",
    "        cls[\"properties\"] = Set{String}()\n",
    "    end\n",
    "    cls[\"properties\"] = Set{String}(cls[\"properties\"])\n",
    "    for prop = cls[\"properties\"]\n",
    "        @assert (prop ∈ keys(properties)) (\"Unknown property \"*prop)\n",
    "    end\n",
    "    \n",
    "end\n",
    "\n",
    "#Specifically classes that are languages (as opposed to e.g. promise problems)\n",
    "language_classes = filter(x->x[2][\"type\"]==\"Language\", classes)\n",
    "parametized_classes = filter(x->x[2][\"type\"]==\"Parameterized Language\", classes)\n",
    "\n",
    "println(\"Initial checks passed\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 37,
   "id": "d822ad2f",
   "metadata": {},
   "outputs": [],
   "source": [
    "#canonical names for classes that have equalities.\n",
    "#if export_for_web talks about choosing canonical representatives, you can add them here to force one way.\n",
    "canonical_forms = [\"PSPACE\",\"BPP\",\"NC\",\"L\",\"NL\",\"NLINSPACE\",\"NC^0\",\"RE\",\"NEXP\",\"SAC^1\",\"coNQP\"];\n",
    "\n",
    "class_lattice = make_language_poset(verbose=false);\n",
    "param_lattice = make_language_poset(parametized_classes; verbose=false);\n",
    "\n",
    "el,vl = LangPoset_to_SimpleDiGraph(class_lattice; verbose=false)\n",
    "tikzplot = TikzGraphs.plot(el, vl, node_style=\"draw\", graph_options=\"nodes={draw,circle}\")\n",
    "TikzPictures.save(TikzPictures.TEX(\"testgraph\"), tikzplot)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 38,
   "id": "3987c2cf",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "KnownType(true, [\"ACC^0⊆TC^0\", \"TC^0⊆NC^1\", \"NC^1⊆NC\", \"NC⊆P\", \"P⊆E\"], 1)\n",
      "KnownType(false, [\"ACC^0⊆nuACC^0\", \"E⊈nuACC^0\"], 3)\n",
      "KnownType(false, [\"PARITY⊈AC^0\"], 3)\n",
      "KnownType(false, [\"PARITY⊈AC^0\", \"PARITY⊂ACC^0\", \"ACC^0⊆TC^0\", \"TC^0⊆NC^1\"], 3)\n",
      "KnownType(true, [\"NL⊆P\", \"P⊆NP\"], 1)\n",
      "false\n",
      "false\n",
      "false\n",
      "false\n"
     ]
    }
   ],
   "source": [
    "println(class_lattice[\"ACC^0\", \"E\"])\n",
    "println(class_lattice[\"E\",\"ACC^0\"])\n",
    "#prints: true => ([\"ACC^0⊆TC^0\", \"TC^0⊆NC^1\", \"NC^1⊆NC\", \"NC⊆P\", \"P⊆E\"] => 1)\n",
    "#        false => ([\"ACC^0⊆nuACC^0\", \"E⊈nuACC^0\"] => 3)\n",
    "#means: \"true\", ACC^0 is contained in E.\n",
    "#       \"false\", E is not contained in ACC^0.\n",
    "#and the reason: we know that ACC^0 is strictly contained in E, and E is strictly in EXP, which is enough. This uses the theorem\n",
    "#named \"E⊈nuACC^0\", which has priority 3 (significantly nontrivial). But showing that ACC^0 ⊆ E only requires \"simple\" facts, so has\n",
    "#priority 1.\n",
    "\n",
    "println(class_lattice[\"PARITY\",\"AC^0\"])\n",
    "#prints: false => ([\"PARITY⊈AC^0\"] => 3)\n",
    "#similar to the above, but we don't know that PARITY contains AC^0 (it doesn't); just that AC^0 doesn't contain PARITY.\n",
    "\n",
    "println(class_lattice[\"NC^1\",\"AC^0\"])\n",
    "#prints: KnownType(false, [\"PARITY⊈AC^0\", \"PARITY⊂ACC^0\", \"ACC^0⊆TC^0\", \"TC^0⊆NC^1\"], 3)\n",
    "#more \"standard\" perspective on the matter, connecting two more common classes.\n",
    "\n",
    "println(class_lattice[\"NL\",\"NP\"])\n",
    "#prints: true => ([\"NL⊆P\", \"P⊆NP\"] => 1)\n",
    "#means: \"true\", NL is contained in NP.\n",
    "#and the reason: we know that NL is contained in P, and P is strictly in NP, which is enough.\n",
    "\n",
    "println( (\"BQP\",\"NP\") ∈ class_lattice)\n",
    "println( (\"NP\",\"BQP\") ∈ class_lattice)\n",
    "println( (\"NP\",\"P\") ∈ class_lattice)\n",
    "println( (\"NP\",\"NL\") ∈ class_lattice)\n",
    "#all print false: none of these are containments we know to be true or false."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 39,
   "id": "5f313f1b-0bdb-47e9-b055-cc4774fe99a4",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Minimal classes: \n",
      "Any[\"PARITY\", \"SF\"]\n",
      "\n",
      "Maximal classes: \n",
      "Any[\"ALL\", \"CZK\"]\n"
     ]
    }
   ],
   "source": [
    "#Find and list all minimal + maximal elements in the lattice. In general, \"ALL\" should be the only maximal element, and\n",
    "#we should have as few minimal elementas possible.\n",
    "minimals = []\n",
    "maximals = []\n",
    "for cls = class_lattice.v\n",
    "    #below() and above() always return the class itself, so we're minimal/maximal if it's the _only_ element.\n",
    "    isMinimal = length(below(class_lattice, cls)) == 1\n",
    "    isMaximal = length(above(class_lattice, cls)) == 1\n",
    "    if isMinimal\n",
    "        push!(minimals, cls)\n",
    "    end\n",
    "    if isMaximal\n",
    "        push!(maximals, cls)\n",
    "    end\n",
    "end\n",
    "\n",
    "println(\"Minimal classes: \")\n",
    "println(minimals)\n",
    "println(\"\\nMaximal classes: \")\n",
    "println(maximals)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "84155e40",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Choosing QAC as representative for "
     ]
    }
   ],
   "source": [
    "open(\"generated/langdata.json\",\"w\") do f\n",
    "    json_export = export_classes_for_web(class_lattice)\n",
    "    JSON.print(f, json_export)\n",
    "end\n",
    "\n",
    "open(\"generated/paramlangdata.json\",\"w\") do f\n",
    "    json_export = export_classes_for_web(param_lattice, \"Parameterized Language\")\n",
    "    JSON.print(f, json_export)\n",
    "end\n",
    "\n",
    "open(\"generated/references.json\",\"w\") do f\n",
    "    json_export = references #no processing\n",
    "    JSON.print(f, json_export)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ba03ad6a",
   "metadata": {},
   "outputs": [],
   "source": [
    "prettyPrint(db)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "475f35ac-ca13-4734-9117-c82703ad2d6f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
